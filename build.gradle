buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Deps.android.plugin
        classpath Deps.kotlin.plugin
    }
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        android {
            compileSdkVersion 29

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 29
                testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            }

            sourceSets.all {
                it.java.srcDirs += it.java.srcDirs.findResults {
                    it.name == 'java' ? "$it.parent/kotlin" : null
                }
            }
        }
    }

    plugins.withType(JacocoPlugin) {
        jacoco.toolVersion = '0.8.4'

        tasks.withType(JacocoReport) {
            check.dependsOn it
            reports.xml.enabled true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
