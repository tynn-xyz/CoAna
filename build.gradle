plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.21' apply false
    id 'org.jetbrains.dokka' version '1.6.21' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

apply from: "$rootDir/publish.gradle"

subprojects {
    pluginManager.withPlugin('org.jetbrains.kotlin.jvm') {
        apply from: "$rootDir/kotlin.gradle"
    }

    pluginManager.withPlugin('maven-publish') {
        apply from: "$rootDir/publish.gradle"
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

dependencyUpdates {
    def isNonStable = { version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
