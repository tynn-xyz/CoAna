//  Copyright 2019 Christian Schmitz
//  SPDX-License-Identifier: Apache-2.0

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61' apply false
    id 'org.jetbrains.dokka' version '0.10.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    plugins.withId('org.jetbrains.dokka') {
        dokka {
            configuration {
                externalDocumentationLink {
                    url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
                    packageListUrl = new URL(url, 'package-list')
                }
            }
        }
    }
}

subprojects {
    group = 'xyz.tynn.coana'

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    plugins.withId('kotlin') {
        project.apply plugin: 'jacoco'
        project.apply plugin: 'maven-publish'

        java.withSourcesJar()
        components.java.addVariantsFromConfiguration(configurations.kdocElements) {}

        publishing {
            publications {
                kotlinJvm(MavenPublication) {
                    from components.java
                }
            }
        }

        dependencies {
            testImplementation Deps.kotlin.test.junit
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        project.apply plugin: 'kotlin-android'

        android {
            compileSdkVersion 29
            buildToolsVersion '29.0.2'

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 29
                testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            }

            sourceSets.all {
                it.java.srcDirs += it.java.srcDirs.findResults {
                    it.name == 'java' ? "$it.parent/kotlin" : null
                }
            }

            compileOptions {
                sourceCompatibility '1.8'
                targetCompatibility '1.8'
            }

            kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        dependencies {
            implementation Deps.kotlin.stdlib
        }
    }

    plugins.withType(JacocoPlugin) {
        jacoco.toolVersion = '0.8.4'

        tasks.withType(JacocoReport) {
            check.dependsOn it
            reports.xml.enabled true
        }
    }

    plugins.withId('org.jetbrains.dokka') {
        rootProject.dokka.subProjects += project.name

        dokka {
            outputFormat = 'html'
            outputDirectory = "$buildDir/kdoc"
        }

        tasks.register('kdocJar', Jar) {
            dependsOn dokka
            classifier 'kdoc'
            from fileTree(dokka.outputDirectory)
        }

        configurations {
            kdocElements {
                artifacts {
                    kdocElements tasks.named('kdocJar')
                }
                attributes {
                    attribute Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION)
                    attribute Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL)
                    attribute DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'kdoc')
                    attribute Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME)
                }
                canBeResolved = false
                canBeConsumed = true
                description = 'kdoc elements for main.'
            }
        }
    }

    plugins.withType(MavenPublishPlugin) {
        project.apply plugin: 'org.jetbrains.dokka'
        project.apply from: "$rootDir/publish.gradle"
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
