plugins {
    id 'com.android.application' version '7.0.4' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.10' apply false
    id 'org.jetbrains.dokka' version '1.6.10' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply from: "$rootDir/publish.gradle"

subprojects {
    pluginManager.withPlugin('org.jetbrains.kotlin.jvm') {
        apply from: "$rootDir/kotlin.gradle"
    }

    pluginManager.withPlugin('maven-publish') {
        apply from: "$rootDir/publish.gradle"
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
